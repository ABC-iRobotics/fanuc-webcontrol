PROGRAM webcontrol
%COMMENT = 'v2'
%NOLOCKGROUP
%NOPAUSE=ERROR+COMMAND+TPENABLE
%NOBUSYLAMP

CONST
	
	PR_WEBJOINTS = 40
	PR_WEBXYZWPR = 41
	R_WEBCOORD = 40	
	R_WEBSTEP = 41

VAR

	STATUS: INTEGER
	return_code: INTEGER
	getstart: STRING[127]
	getcpos: STRING[127]
	getstep: STRING[127]
	
	coordpos: REAL
	movestep: REAL

	j1: STRING[4]
	j2: STRING[4]
	j3: STRING[4]
	j4: STRING[4]
	j5: STRING[4]
	j6: STRING[4]

--Set the current JOINT position as initial position and jogging

ROUTINE setcurjpos

VAR

	cur_jpos: JOINTPOS

BEGIN
	
	cur_jpos = CURJPOS(0,0)
	
	SET_JPOS_REG(PR_WEBJOINTS, cur_jpos, STATUS)
	
	IF (STATUS <> 0 ) THEN
	
		WRITE ('SET_JPOS_REG  built-IN failed WITH STATUS = ', STATUS, CR)
	
	ELSE

		FLG[2] = ON
		FLG[1] = ON
		
		IF FLG[2] AND FLG[1] THEN
			WRITE('FLG2 and FLG1 are ON', CR)
		ENDIF
	
	ENDIF

END setcurjpos

--Set the current XYZWPR position as initial position and move

ROUTINE setcurxyz

VAR

	cur_pos: XYZWPR

BEGIN
	
	cur_pos = CURPOS(0,0)
	
	SET_POS_REG(PR_WEBXYZWPR, cur_pos, STATUS)
	
	IF (STATUS <> 0 ) THEN
	
		WRITE ('SET_POS_REG  built-IN failed WITH STATUS = ', STATUS, CR)
	
	ELSE

		FLG[3] = ON
		FLG[1] = ON
		
		IF FLG[3] AND FLG[1] THEN
			WRITE('FLG3 and FLG1 are ON', CR)
		ENDIF
	
	ENDIF

END setcurxyz

--Set the current TCP position as initial position and rotating

ROUTINE setcurwpr

VAR

	cur_pos: XYZWPR

BEGIN
	
	cur_pos = CURPOS(0,0)
	
	SET_POS_REG(PR_WEBXYZWPR, cur_pos, STATUS)
	
	IF (STATUS <> 0 ) THEN
	
		WRITE ('SET_POS_REG  built-IN failed WITH STATUS = ', STATUS, CR)
	
	ELSE

		FLG[4] = ON
		FLG[1] = ON

		IF FLG[4] AND FLG[1] THEN
			WRITE('FLG4 and FLG1 are ON', CR)
		ENDIF
	
	ENDIF

END setcurwpr

--Move to predefined pose

ROUTINE setpose

VAR 

	jpos: JOINTPOS
	jposVal: ARRAY[9] OF REAL

BEGIN

	CNV_STR_REAL(j1, jposVal[1]) 
	CNV_STR_REAL(j2, jposVal[2])
	CNV_STR_REAL(j3, jposVal[3])
	CNV_STR_REAL(j4, jposVal[4])
	CNV_STR_REAL(j5, jposVal[5])
	CNV_STR_REAL(j6, jposVal[6])
	jposVal[7] = 0
	jposVal[8] = 0
	jposVal[9] = 0
	
	CNV_REL_JPOS(jposVal, jpos, STATUS)

	IF (STATUS <> 0 ) THEN
	
		WRITE ('CNV_REL_JPOS built-IN failed WITH STATUS = ', STATUS, CR)
	
	ELSE

		SET_JPOS_REG(PR_WEBJOINTS, jpos, STATUS)
		
		IF (STATUS <> 0 ) THEN
		
			WRITE ('SET_JPOS_REG  built-IN failed WITH STATUS = ', STATUS, CR)
		
		ELSE		
		
			FLG[5] = ON
			FLG[1] = ON
	
			IF FLG[5] AND FLG[1] THEN
				WRITE('FLG5 and FLG1 are ON', CR)
			ENDIF
			
		ENDIF
	ENDIF

END setpose

--The main program

BEGIN

	IF (getstart = 'j') THEN
		CNV_STR_REAL(getcpos, coordpos)
		CNV_STR_REAL(getstep, movestep)
		SET_REAL_REG(R_WEBCOORD, coordpos, STATUS)
		SET_REAL_REG(R_WEBSTEP, movestep, STATUS)
		setcurjpos
		return_code = 204
	ENDIF

	IF (getstart = 'l') THEN
		CNV_STR_REAL(getcpos, coordpos)
		CNV_STR_REAL(getstep, movestep)		
		SET_REAL_REG(R_WEBCOORD, coordpos, STATUS)
		SET_REAL_REG(R_WEBSTEP, movestep, STATUS)
		setcurxyz
		return_code = 204
	ENDIF

	IF (getstart = 'r') THEN
		CNV_STR_REAL(getcpos, coordpos)
		CNV_STR_REAL(getstep, movestep)		
		SET_REAL_REG(R_WEBCOORD, coordpos, STATUS)
		SET_REAL_REG(R_WEBSTEP, movestep, STATUS)
		setcurwpr
		return_code = 204
	ENDIF
	
	IF getstart = 'p' THEN
		setpose
		return_code = 204
	ENDIF
	
END webcontrol