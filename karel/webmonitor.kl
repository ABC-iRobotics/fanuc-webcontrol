PROGRAM webmonitor
%COMMENT = 'v5'
%NOLOCKGROUP
%NOPAUSE=ERROR+COMMAND+TPENABLE
%NOBUSYLAMP

CONST

	R_WEBXMAX = 43	
	R_WEBYMAX = 44
	R_WEBZMAX = 45
	R_WEBXMIN = 46
	R_WEBYMIN = 47
	R_WEBZMIN = 48

VAR

	STATUS: INTEGER
	responseFile: FILE

	xmax_flag: BOOLEAN
	ymax_flag: BOOLEAN
	zmax_flag: BOOLEAN
	xmin_flag: BOOLEAN
	ymin_flag: BOOLEAN
	zmin_flag: BOOLEAN
	
	xmax_int: INTEGER
	ymax_int: INTEGER
	zmax_int: INTEGER
	xmin_int: INTEGER
	ymin_int: INTEGER
	zmin_int: INTEGER
	
	xmax_real: REAL
	ymax_real: REAL
	zmax_real: REAL
	xmin_real: REAL
	ymin_real: REAL
	zmin_real: REAL
	
	error_code: INTEGER
	error_string: STRING[127]
	cause_code: INTEGER
	cause_string: STRING[127]
	time_int: INTEGER
	severity: INTEGER
	prog_nam: STRING[127]

	gettask IN DRAM FROM webstart: STRING[127]
	task_att: INTEGER
	task_dum: STRING[127]
	running: INTEGER
	running_task: INTEGER

	currentPos: XYZWPREXT
	currentPosJ: JOINTPOS
	valuePosJ: ARRAY[6] OF REAL
	
	reach_ab IN DRAM FROM webcheck: INTEGER

	limit_x IN DRAM FROM webcheck: INTEGER
	limit_y IN DRAM FROM webcheck: INTEGER
	limit_z IN DRAM FROM webcheck: INTEGER


BEGIN

	IF UNINIT( gettask ) THEN gettask = '@@@###@@@'; ENDIF

	IF UNINIT( reach_ab ) THEN reach_ab = 0; ENDIF

	IF UNINIT( limit_x ) THEN limit_x = 0; ENDIF
	IF UNINIT( limit_y ) THEN limit_y = 0; ENDIF
	IF UNINIT( limit_z ) THEN limit_z = 0; ENDIF

	IF UNINIT( valuePosJ[1] ) THEN valuePosJ[1] = 100000; ENDIF
	IF UNINIT( valuePosJ[2] ) THEN valuePosJ[2] = 100000; ENDIF
	IF UNINIT( valuePosJ[3] ) THEN valuePosJ[3] = 100000; ENDIF
	IF UNINIT( valuePosJ[4] ) THEN valuePosJ[4] = 100000; ENDIF
	IF UNINIT( valuePosJ[5] ) THEN valuePosJ[5] = 100000; ENDIF
	IF UNINIT( valuePosJ[6] ) THEN valuePosJ[6] = 100000; ENDIF

	GET_REG(R_WEBXMAX, xmax_flag, xmax_int, xmax_real, STATUS)
	GET_REG(R_WEBYMAX, ymax_flag, ymax_int, ymax_real, STATUS)
	GET_REG(R_WEBZMAX, zmax_flag, zmax_int, zmax_real, STATUS)
	GET_REG(R_WEBXMIN, xmin_flag, xmin_int, xmin_real, STATUS)
	GET_REG(R_WEBYMIN, ymin_flag, ymin_int, ymin_real, STATUS)
	GET_REG(R_WEBZMIN, zmin_flag, zmin_int, zmin_real, STATUS)

	ERR_DATA(MAXINT, error_code, error_string, cause_code, cause_string, time_int, severity, prog_nam)

	running = 0
	running_task = 0
	
	GET_TSK_INFO(gettask, 0, TSK_STATUS, task_att, task_dum, STATUS)
	IF (STATUS <> 0 ) THEN
		WRITE ('GET_TSK_INFO is failed, STATUS = ', STATUS, CR)
	ELSE
		IF ( task_att = PG_RUNNING ) THEN
			running = 1
		ELSE
			running = 0
		ENDIF
	ENDIF

	IF (gettask = 'WEBMOTION    TP') OR (gettask = 'WEBMOTION') THEN
		running_task = 1
	ELSE
		running_task = 0
	ENDIF
	
	currentPos = CURPOS(0,0)
	
	currentPosJ = CURJPOS(0,0)
	
	CNV_JPOS_REL( currentPosJ, valuePosJ, STATUS )
	IF (STATUS <> 0 ) THEN
		WRITE ('CNV_JPOS_REL is failed, STATUS = ', STATUS, CR)
	ENDIF

	OPEN FILE responseFile ('RW', 'RD:RESPONSE.HTM')
		
		WRITE responseFile( '{', CR )
			WRITE responseFile( '"joint": [' )
				WRITE responseFile( valuePosJ[1],',') 
				WRITE responseFile( valuePosJ[2],',')
				WRITE responseFile( valuePosJ[3],',')
				WRITE responseFile( valuePosJ[4],',')
				WRITE responseFile( valuePosJ[5],',')
				WRITE responseFile( valuePosJ[6] )
			WRITE responseFile( ' ],', CR )
			WRITE responseFile( '"pose": [' )
				WRITE responseFile( currentPos.x,',')  
				WRITE responseFile( currentPos.y,',')
				WRITE responseFile( currentPos.z,',')
				WRITE responseFile( currentPos.w,',')
				WRITE responseFile( currentPos.p,',')
				WRITE responseFile( currentPos.r )
			WRITE responseFile( ' ],', CR )
			WRITE responseFile( '"limit": [' )
				WRITE responseFile( xmax_real,',')  
				WRITE responseFile( ymax_real,',')
				WRITE responseFile( zmax_real,',')
				WRITE responseFile( xmin_real,',')
				WRITE responseFile( ymin_real,',')
				WRITE responseFile( zmin_real )
			WRITE responseFile( ' ] ,', CR )
			WRITE responseFile( '"status": [' )
				WRITE responseFile( running,',')
				WRITE responseFile( running_task )
			WRITE responseFile( ' ] ,', CR )
			WRITE responseFile( '"message": [' )
				WRITE responseFile( reach_ab,',')
				WRITE responseFile( limit_x,',')
				WRITE responseFile( limit_y,',')
				WRITE responseFile( limit_z)
			WRITE responseFile( ' ],', CR )			
			WRITE responseFile( '"error": [' )
				WRITE responseFile( error_code,',' )
				WRITE responseFile( '"',error_string,'"' )
			WRITE responseFile( ' ] ', CR )
		WRITE responseFile( '}')

	CLOSE FILE responseFile

END webmonitor